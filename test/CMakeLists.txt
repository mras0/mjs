enable_testing()
add_custom_target(check COMMAND ${CMAKE_CTEST_COMMAND} --force-new-ctest-process --output-on-failure --build-config "$<CONFIGURATION>")

macro(mjs_add_normal_test name)
    add_executable(${name} ${name}.cpp ${ARGN})
    target_link_libraries(${name} test_lib)
    add_test(${name} ${name})
    add_dependencies(check ${name})
endmacro()

macro(mjs_add_file_test version filename)
    string(REGEX REPLACE "\\." "_" name "${version}_${filename}")
    add_test(NAME ${name} COMMAND mjs -${version} "${CMAKE_CURRENT_SOURCE_DIR}/js/${filename}")
    add_dependencies(check mjs)
    set_tests_properties(${name} PROPERTIES ${ARGN})
endmacro()

add_library(test_lib STATIC test_spec.cpp test_spec.h test.cpp test.h)
target_link_libraries(test_lib mjs_lib)

mjs_add_normal_test(value_test)
mjs_add_normal_test(lexer_test)
mjs_add_normal_test(parser_test)
mjs_add_normal_test(interpreter_test)
mjs_add_normal_test(util_test)
mjs_add_normal_test(array_object_test)
mjs_add_normal_test(regexp_object_test)

mjs_add_file_test(es1 array_literal.js WILL_FAIL TRUE)
mjs_add_file_test(es3 array_literal.js PASS_REGULAR_EXPRESSION "OK")
mjs_add_file_test(es3 main.js PASS_REGULAR_EXPRESSION "OK")
mjs_add_file_test(es5 main.js PASS_REGULAR_EXPRESSION "OK")
